#!/usr/bin/perl

# A Hobbit client-side module to check the local ntpd daemon
# synchronization status.  Not to be confused with the built-in "ntp"
# test, which checks the ntpd server remotely.

# Copyright (C) 2008 Peter Eisentraut <petere@debian.org>
#
# Permission is hereby granted, free of charge, to any person
# obtaining a copy of this software and associated documentation
# files (the "Software"), to deal in the Software without
# restriction, including without limitation the rights to use,
# copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following
# conditions:
#
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
# OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
# HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
# WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
# OTHER DEALINGS IN THE SOFTWARE.

use warnings;
use strict;
use Hobbit;

if (! -x '/usr/bin/ntpq' and system('ntpq -? >/dev/null 2>&1') != 0) {
	exit 0;
}

my $bb = new Hobbit('ntpq');

$bb->print("NTP peers:\n\n");

my $found_syspeer = 0;
my $recently_started = (time - (stat('/var/run/ntpd.pid'))[9]) < 300;
my $initializing = 0;

my @output = `ntpq -np 2>&1` or die;
foreach my $line (@output) {
	if ($line =~ /^  / or $line =~ /^==/) {
		$bb->print('   ' . $line);
	} elsif ($line =~ /^\*/) {
		$bb->color_line('green', $line);
		$found_syspeer = 1;
	} elsif ($line =~ /^( |x|\.|\-|\+|\#|o)/) {
		$bb->color_line('clear', $line);
		$initializing = 1 if $line =~ /\.INIT\.|\.STEP\./;
	} else {
		$bb->color_line('yellow', $line);
	}
}

if (!$found_syspeer) {
	$bb->print("\n");
	if ($recently_started and $initializing) {
		$bb->color_line('yellow', "No system peer entry (\"*\") found; ntpd was recently started and is initializing.\n");
	} else {
		$bb->color_line('red', "No system peer entry (\"*\") found\n");
	}
}

$bb->send;
